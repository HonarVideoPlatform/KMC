<?xml version="1.0" encoding="utf-8"?>
<!--- Category selection tool for analytics 
 @see com.kaltura.kmc.modules.analytics.view.Filter
-->
<HelpTitleWindow xmlns="com.kaltura.containers.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="350" height="450"
				 xmlns:view="com.kaltura.kmc.modules.analytics.view.*" showCloseButton="true" close="closeWindow(event)"
				 title="{resourceManager.getString('analytics', 'categoriesTitle')}" showHelpButton="false" xmlns:cat="com.kaltura.edw.components.fltr.cat.*">
	
	<mx:Metadata>
		
		[ResourceBundle("filter")]
		
		/**
		 * dispatched when "submit" button is clicked
		 * */
		[Event(name="filterByCategories",type="flash.events.Event")]
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import com.kaltura.dataStructures.HashMap;
			import com.kaltura.edw.components.fltr.FilterComponentEvent;
			import com.kaltura.edw.components.fltr.cat.CatTreeSelectionMode;
			import com.kaltura.edw.components.fltr.indicators.IndicatorVo;
			import com.kaltura.edw.vo.CategoryVO;
			import com.kaltura.kmc.business.JSGate;
			import com.kaltura.kmc.modules.analytics.business.AnalyticsCategoriesDataManager;
			import com.kaltura.kmc.modules.analytics.model.AnalyticsModelLocator;
			import com.kaltura.vo.KalturaCategory;
			
			import mx.collections.ArrayCollection;
			import mx.managers.PopUpManager;
			
			
			public static const FILTER_BY_CATEGORIES:String = "filterByCategories";
			
			
			[Bindable]
			private var _model:AnalyticsModelLocator = AnalyticsModelLocator.getInstance();
			
			
			/**
			 * elements are CategoryVOs.
			 * an actual reference to the same ArrayCollection the Filter uses 
			 * */
			private var _selectedCategories:ArrayCollection;

			public function get selectedCategories():ArrayCollection {
				return _selectedCategories;
			}

			public function set selectedCategories(selectedCategoriesArr:ArrayCollection):void {
				_selectedCategories = selectedCategoriesArr;
			}


			/**
			 * close window event - when pressing the X button, cancel button or after saving the data
			 */
			private function closeWindow(event:Event = null):void {
				PopUpManager.removePopUp(this);
				JSGate.maskHeader(true);
				catTree.filter = '';
			}


			/**
			 * change the contents of the AC to match user selection
			 * */
			private function onSubmitClick(event:Event):void {
				_selectedCategories.removeAll();
				if (catTree.filter) {
					var catNames:Array = catTree.filter.split(',');
					var catsMap:HashMap = _model.categoriesMap;
					for (var i:int = 0; i<catNames.length; i++) {
						if (!catNames[i]) continue;
						for each (var catvo:CategoryVO in catsMap) {
							if (catvo.id == catNames[i]) {
								_selectedCategories.addItem(catvo);
								break;
							}
						}
					}
				}

				dispatchEvent(new Event(CategoriesTreePopUp.FILTER_BY_CATEGORIES));
				closeWindow();
			}


			private function onTreeCreationComplete():void {
				var categories:String = '';
				for each (var catvo:CategoryVO in _selectedCategories) {
					categories += catvo.id + ",";
				}
				catTree.filter = categories;
			}
			
			private function catTree_valueChangeHandler(event:FilterComponentEvent):void
			{
				var ivo:IndicatorVo = event.data;
				// if category doesn't exist in catMap, create a fake category with data available on ivo and put it there. 
				// this way onSubmitClick will find it there, and hopefully use it wisely.
				var catvo:CategoryVO = _model.categoriesMap.getValue(ivo.value.toString());
				if (!catvo){
					var kCat:KalturaCategory = new KalturaCategory();
					kCat.id = ivo.value;
					kCat.name = ivo.label;
					kCat.fullName = ivo.tooltip;
					
					_model.categoriesMap.put(kCat.id + "", new CategoryVO(kCat.id, kCat.name, kCat));
				}
				
			}
			
		]]>
	</mx:Script>
	
	<cat:CatTreeWrapper id="catTree" height="100%" width="100%" 
						selectionMode="{CatTreeSelectionMode.MULTIPLE_SELECT_EXACT}" valueChange="catTree_valueChangeHandler(event)"
						dataProvider="{_model.categories}" categories="{_model.categoriesMap}" kClient="{_model.kc}" 
						externalDataManager="{new AnalyticsCategoriesDataManager()}" chunkedData="{false}"
						creationComplete="onTreeCreationComplete()"/> <!--chunkedData="{filterModel.chunkedCategoriesLoad}"-->

	<mx:ControlBar width="100%" horizontalAlign="center" paddingBottom="2" paddingLeft="2" paddingRight="2" paddingTop="2">
		<mx:Button label="{resourceManager.getString('analytics', 'filter')}" useHandCursor="true" buttonMode="true" id="submitBtn" click="onSubmitClick(event)"/>
	</mx:ControlBar>
</HelpTitleWindow>
